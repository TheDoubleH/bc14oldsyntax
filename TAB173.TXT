OBJECT Table 173 Standard Purchase Code
{
  OBJECT-PROPERTIES
  {
    Date=12/01/21;
    Time=12:00:00 PM;
    Version List=NAVW114.31;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=VAR
               StandardVendorPurchaseCode@1000 : Record 175;
               ConfirmManagement@1002 : Codeunit 27;
             BEGIN
               StandardVendorPurchaseCode.RESET;
               StandardVendorPurchaseCode.SETRANGE(Code,Code);
               IF NOT StandardVendorPurchaseCode.ISEMPTY THEN
                 IF NOT ConfirmManagement.ConfirmProcess(
                      STRSUBSTNO(StdPurchCodeDeletionQst,Code,StandardVendorPurchaseCode.TABLECAPTION),TRUE)
                 THEN
                   ERROR('');

               StdPurchLine.RESET;
               StdPurchLine.SETRANGE("Standard Purchase Code",Code);
               StdPurchLine.DELETEALL(TRUE);

               StandardVendorPurchaseCode.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Standard Purchase Code;
    LookupPageID=Page177;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=VAR
                                                                Currency@1000 : Record 4;
                                                                Currency2@1001 : Record 4;
                                                                ConfirmManagement@1002 : Codeunit 27;
                                                              BEGIN
                                                                IF NOT Currency.GET("Currency Code") THEN
                                                                  Currency.InitRoundingPrecision;
                                                                IF NOT Currency2.GET(xRec."Currency Code") THEN
                                                                  Currency2.InitRoundingPrecision;

                                                                IF Currency."Amount Rounding Precision" <> Currency2."Amount Rounding Precision" THEN BEGIN
                                                                  StdPurchLine.RESET;
                                                                  StdPurchLine.SETRANGE("Standard Purchase Code",Code);
                                                                  StdPurchLine.SETRANGE(Type,StdPurchLine.Type::"G/L Account");
                                                                  StdPurchLine.SETFILTER("Amount Excl. VAT",'<>%1',0);
                                                                  IF StdPurchLine.FIND('-') THEN BEGIN
                                                                    IF NOT ConfirmManagement.ConfirmProcess(
                                                                         STRSUBSTNO(
                                                                           Text001,FIELDCAPTION("Currency Code"),StdPurchLine.FIELDCAPTION("Amount Excl. VAT"),
                                                                           FIELDCAPTION("Currency Code")),TRUE)
                                                                    THEN
                                                                      ERROR(Text002);
                                                                    REPEAT
                                                                      StdPurchLine."Amount Excl. VAT" :=
                                                                        ROUND(StdPurchLine."Amount Excl. VAT",Currency."Amount Rounding Precision");
                                                                      StdPurchLine.MODIFY;
                                                                    UNTIL StdPurchLine.NEXT = 0;
                                                                  END;
                                                                END;
                                                                // MODIFY;
                                                              END;

                                                   CaptionML=ENU=Currency Code }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StdPurchLine@1000 : Record 174;
      Text001@1003 : TextConst 'ENU=If you change the %1, the %2 will be rounded according to the new %3.';
      Text002@1002 : TextConst 'ENU=The update has been interrupted to respect the warning.';
      StdPurchCodeDeletionQst@1001 : TextConst '@@@="%1=Standard Purchase Code, %2=Table Caption";ENU=If you delete the code %1, the related records in the %2 table will also be deleted. Do you want to continue?';

    BEGIN
    END.
  }
}

