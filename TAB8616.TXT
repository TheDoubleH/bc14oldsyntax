OBJECT Table 8616 Config. Package Field
{
  OBJECT-PROPERTIES
  {
    Date=03/26/21;
    Time=12:00:00 PM;
    Version List=NAVW114.24;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               DeleteConfigFieldMapping;
             END;

    ReplicateData=No;
    CaptionML=ENU=Config. Package Field;
  }
  FIELDS
  {
    { 1   ;   ;Package Code        ;Code20        ;TableRelation="Config. Package";
                                                   CaptionML=ENU=Package Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=ENU=Table ID;
                                                   NotBlank=Yes }
    { 3   ;   ;Field ID            ;Integer       ;TableRelation=IF (Dimension=CONST(No)) Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=ENU=Field ID;
                                                   NotBlank=Yes }
    { 4   ;   ;Field Name          ;Text30        ;OnValidate=BEGIN
                                                                "XML Field Name" := GetUniqueElementName("Field Name");
                                                              END;

                                                   CaptionML=ENU=Field Name }
    { 5   ;   ;Field Caption       ;Text250       ;CaptionML=ENU=Field Caption }
    { 6   ;   ;Validate Field      ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT Dimension THEN BEGIN
                                                                  IF xRec."Validate Field" AND NOT "Validate Field" AND "Primary Key" THEN
                                                                    ERROR(Text000,"Field Caption");
                                                                  IF "Validate Field" THEN
                                                                    ThrowErrorIfFieldRemoved;
                                                                  UpdateFieldErrors;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Validate Field }
    { 7   ;   ;Include Field       ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT Dimension THEN BEGIN
                                                                  IF xRec."Include Field" AND NOT "Include Field" AND "Primary Key" THEN
                                                                    ERROR(Text000,"Field Caption");
                                                                  IF "Include Field" THEN
                                                                    ThrowErrorIfFieldRemoved;
                                                                  "Validate Field" := "Include Field";
                                                                  UpdateFieldErrors;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Include Field }
    { 8   ;   ;Localize Field      ;Boolean       ;CaptionML=ENU=Localize Field }
    { 9   ;   ;Relation Table ID   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Relation Table ID;
                                                   Editable=No }
    { 10  ;   ;Relation Table Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Relation Table ID)));
                                                   CaptionML=ENU=Relation Table Caption;
                                                   Editable=No }
    { 11  ;   ;Dimension           ;Boolean       ;CaptionML=ENU=Dimension;
                                                   Editable=No }
    { 12  ;   ;Primary Key         ;Boolean       ;CaptionML=ENU=Primary Key;
                                                   Editable=No }
    { 13  ;   ;Processing Order    ;Integer       ;CaptionML=ENU=Processing Order }
    { 14  ;   ;Create Missing Codes;Boolean       ;OnValidate=BEGIN
                                                                IF "Create Missing Codes" THEN
                                                                  TESTFIELD("Relation Table ID");
                                                              END;

                                                   CaptionML=ENU=Create Missing Codes }
    { 15  ;   ;Mapping Exists      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Config. Field Mapping" WHERE (Package Code=FIELD(Package Code),
                                                                                                    Table ID=FIELD(Table ID),
                                                                                                    Field ID=FIELD(Field ID)));
                                                   CaptionML=ENU=Mapping Exists;
                                                   Editable=No }
    { 16  ;   ;AutoIncrement       ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Primary Key",FALSE);
                                                                TestFieldIsInteger;
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=ENU=AutoIncrement }
    { 20  ;   ;XML Field Name      ;Text30        ;DataClassification=SystemMetadata;
                                                   CaptionML=ENU=XML Field Name }
  }
  KEYS
  {
    {    ;Package Code,Table ID,Field ID          ;Clustered=Yes }
    {    ;Package Code,Table ID,Include Field      }
    {    ;Package Code,Table ID,Validate Field     }
    {    ;Package Code,Table ID,Processing Order   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 is part of the primary key and must be included.';
      Text001@1001 : TextConst 'ENU=%1 value ''%2'' does not exist.';
      Text002@1003 : TextConst 'ENU=Updating validation errors';
      MustBeIntegersErr@1002 : TextConst 'ENU=must be Integer or BigInteger';

    LOCAL PROCEDURE UpdateFieldErrors@1000();
    VAR
      ConfigPackageData@1002 : Record 8615;
      ConfigPackageField@1007 : Record 8616;
      TempConfigPackageTable@1004 : TEMPORARY Record 8613;
      ConfigPackageRecord@1008 : Record 8614;
      ConfigPackageMgt@1000 : Codeunit 8611;
      ConfigValidateMgt@1005 : Codeunit 8617;
      ConfigProgressBar@1006 : Codeunit 8615;
      RecRef@1001 : RecordRef;
      FieldRef@1003 : FieldRef;
    BEGIN
      IF NOT Dimension THEN BEGIN
        IF "Include Field" THEN BEGIN
          RecRef.OPEN("Table ID",TRUE);
          FieldRef := RecRef.FIELD("Field ID");
        END;
        ConfigPackageData.SETRANGE("Package Code","Package Code");
        ConfigPackageData.SETRANGE("Table ID","Table ID");
        ConfigPackageData.SETRANGE("Field ID","Field ID");
        IF ConfigPackageData.FINDSET THEN BEGIN
          ConfigProgressBar.Init(ConfigPackageData.COUNT,1,Text002);
          REPEAT
            ConfigProgressBar.Update(ConfigPackageData.Value);
            ConfigPackageRecord.GET(
              ConfigPackageData."Package Code",ConfigPackageData."Table ID",ConfigPackageData."No.");
            ConfigPackageMgt.CleanFieldError(ConfigPackageData);
            IF "Include Field" THEN BEGIN
              ConfigPackageMgt.FieldError(ConfigPackageData,ConfigValidateMgt.EvaluateValue(FieldRef,ConfigPackageData.Value,FALSE),0);
              IF "Validate Field" THEN BEGIN
                CLEAR(TempConfigPackageTable);
                ConfigPackageField.INIT;
                ConfigPackageField.RESET;
                ConfigPackageField.SETRANGE("Package Code","Package Code");
                ConfigPackageField.SETRANGE("Table ID","Table ID");
                ConfigPackageField.SETRANGE("Field ID","Field ID");
                IF NOT ConfigPackageMgt.ValidateFieldRelationInRecord(
                     ConfigPackageField,TempConfigPackageTable,ConfigPackageRecord,RecRef)
                THEN
                  ConfigPackageMgt.FieldError(ConfigPackageData,STRSUBSTNO(Text001,FieldRef.CAPTION,ConfigPackageData.Value),0);
              END;
            END;
          UNTIL ConfigPackageData.NEXT = 0;
          ConfigProgressBar.Close;
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteConfigFieldMapping@1();
    VAR
      ConfigFieldMapping@1000 : Record 8628;
    BEGIN
      ConfigFieldMapping.SETRANGE("Package Code","Package Code");
      ConfigFieldMapping.SETRANGE("Table ID","Table ID");
      ConfigFieldMapping.SETRANGE("Field ID","Field ID");
      ConfigFieldMapping.DELETEALL;
    END;

    [External]
    PROCEDURE GetRelationTablesID@2() Result : Text;
    VAR
      TableRelationsMetadata@1002 : Record 2000000141;
    BEGIN
      TableRelationsMetadata.SETRANGE("Table ID","Table ID");
      TableRelationsMetadata.SETRANGE("Field No.","Field ID");
      IF TableRelationsMetadata.FINDSET THEN
        REPEAT
          Result += FORMAT(TableRelationsMetadata."Related Table ID") + '|';
        UNTIL TableRelationsMetadata.NEXT = 0;
      EXIT(DELCHR(Result,'>','|'));
    END;

    LOCAL PROCEDURE TestFieldIsInteger@4();
    VAR
      Field@1000 : Record 2000000041;
    BEGIN
      IF Field.GET("Table ID","Field ID") THEN
        IF NOT (Field.Type IN [Field.Type::BigInteger,Field.Type::Integer]) THEN
          Field.FIELDERROR(Type,MustBeIntegersErr);
    END;

    LOCAL PROCEDURE ThrowErrorIfFieldRemoved@3();
    VAR
      Field@1001 : Record 2000000041;
      TypeHelper@1000 : Codeunit 10;
    BEGIN
      Field.GET("Table ID","Field ID");
      TypeHelper.TestFieldIsNotObsolete(Field);
    END;

    PROCEDURE GetElementName@6() : Text[250];
    VAR
      ConfigXMLExchange@1000 : Codeunit 8614;
    BEGIN
      IF "XML Field Name" <> '' THEN
        EXIT("XML Field Name");

      EXIT(ConfigXMLExchange.GetElementName("Field Name"));
    END;

    PROCEDURE GetValidatedElementName@7() : Text[250];
    VAR
      ConfigValidateMgt@1000 : Codeunit 8617;
    BEGIN
      EXIT(ConfigValidateMgt.CheckName(GetElementName))
    END;

    LOCAL PROCEDURE GetUniqueElementName@8(FieldName@1004 : Text[30]) : Text[30];
    VAR
      ConfigPackageField@1000 : Record 8616;
      ConfigXMLExchange@1001 : Codeunit 8614;
      ElementName@1002 : Text[30];
      NewFieldName@1003 : Text;
    BEGIN
      ElementName := COPYSTR(ConfigXMLExchange.GetElementName(FieldName),1,MAXSTRLEN(ElementName));
      ConfigPackageField.SETRANGE("Package Code","Package Code");
      ConfigPackageField.SETRANGE("Table ID","Table ID");
      ConfigPackageField.SETFILTER("Field ID",'<>%1',"Field ID");
      ConfigPackageField.SETRANGE("XML Field Name",ElementName);
      IF NOT ConfigPackageField.ISEMPTY THEN BEGIN
        NewFieldName := INCSTR(FieldName);
        IF NewFieldName = '' THEN
          NewFieldName := FieldName + '1';
        EXIT(GetUniqueElementName(COPYSTR(NewFieldName,1,MAXSTRLEN(FieldName))));
      END;

      EXIT(ElementName);
    END;

    BEGIN
    END.
  }
}

