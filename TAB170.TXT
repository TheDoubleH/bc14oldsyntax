OBJECT Table 170 Standard Sales Code
{
  OBJECT-PROPERTIES
  {
    Date=12/01/21;
    Time=12:00:00 PM;
    Version List=NAVW114.31;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=VAR
               StandardCustomerSalesCode@1001 : Record 172;
               ConfirmManagement@1000 : Codeunit 27;
             BEGIN
               StandardCustomerSalesCode.RESET;
               StandardCustomerSalesCode.SETRANGE(Code,Code);
               IF NOT StandardCustomerSalesCode.ISEMPTY THEN
                 IF NOT ConfirmManagement.ConfirmProcess(
                      STRSUBSTNO(StdSalesCodeDeletionQst,Code,StandardCustomerSalesCode.TABLECAPTION),TRUE)
                 THEN
                   ERROR('');

               StdSalesLine.RESET;
               StdSalesLine.SETRANGE("Standard Sales Code",Code);
               StdSalesLine.DELETEALL(TRUE);

               StandardCustomerSalesCode.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Standard Sales Code;
    LookupPageID=Page172;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=VAR
                                                                Currency@1000 : Record 4;
                                                                Currency2@1001 : Record 4;
                                                                ConfirmManagement@1002 : Codeunit 27;
                                                              BEGIN
                                                                IF NOT Currency.GET("Currency Code") THEN
                                                                  Currency.InitRoundingPrecision;
                                                                IF NOT Currency2.GET(xRec."Currency Code") THEN
                                                                  Currency2.InitRoundingPrecision;

                                                                IF Currency."Amount Rounding Precision" <> Currency2."Amount Rounding Precision" THEN BEGIN
                                                                  StdSalesLine.RESET;
                                                                  StdSalesLine.SETRANGE("Standard Sales Code",Code);
                                                                  StdSalesLine.SETRANGE(Type,StdSalesLine.Type::"G/L Account");
                                                                  StdSalesLine.SETFILTER("Amount Excl. VAT",'<>%1',0);
                                                                  IF StdSalesLine.FIND('-') THEN BEGIN
                                                                    IF NOT ConfirmManagement.ConfirmProcess(
                                                                         STRSUBSTNO(
                                                                           Text001,FIELDCAPTION("Currency Code"),StdSalesLine.FIELDCAPTION("Amount Excl. VAT"),
                                                                           FIELDCAPTION("Currency Code")),TRUE)
                                                                    THEN
                                                                      ERROR(Text002);
                                                                    REPEAT
                                                                      StdSalesLine."Amount Excl. VAT" :=
                                                                        ROUND(StdSalesLine."Amount Excl. VAT",Currency."Amount Rounding Precision");
                                                                      StdSalesLine.MODIFY;
                                                                    UNTIL StdSalesLine.NEXT = 0;
                                                                  END;
                                                                END;
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Currency Code }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StdSalesLine@1000 : Record 171;
      Text001@1002 : TextConst 'ENU=If you change the %1, the %2 will be rounded according to the new %3.';
      Text002@1003 : TextConst 'ENU=The update has been interrupted to respect the warning.';
      StdSalesCodeDeletionQst@1001 : TextConst '@@@="%1=Standard Sales Code, %2=Table Caption";ENU=If you delete the code %1, the related records in the %2 table will also be deleted. Do you want to continue?';

    BEGIN
    END.
  }
}

