OBJECT Table 5877 Phys. Invt. Record Header
{
  OBJECT-PROPERTIES
  {
    Date=03/24/19;
    Time=12:00:00 PM;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Order No.,Recording No.,Description;
    OnInsert=BEGIN
               TESTFIELD("Order No.");
               PhysInvtOrderHeader.GET("Order No.");
               PhysInvtOrderHeader.TESTFIELD(Status,PhysInvtOrderHeader.Status::Open);

               IF "Recording No." = 0 THEN BEGIN
                 LOCKTABLE;
                 PhysInvtRecordHeader.RESET;
                 PhysInvtRecordHeader.SETRANGE("Order No.","Order No.");
                 IF PhysInvtRecordHeader.FINDLAST THEN
                   "Recording No." := PhysInvtRecordHeader."Recording No." + 1
                 ELSE
                   "Recording No." := 1;
               END;
             END;

    OnModify=BEGIN
               TESTFIELD(Status,Status::Open);
             END;

    OnDelete=BEGIN
               TESTFIELD(Status,Status::Open);

               PhysInvtRecordLine.RESET;
               PhysInvtRecordLine.SETRANGE("Order No.","Order No.");
               PhysInvtRecordLine.SETRANGE("Recording No.","Recording No.");
               PhysInvtRecordLine.DELETEALL(TRUE);

               PhysInvtCommentLine.RESET;
               PhysInvtCommentLine.SETRANGE("Document Type",PhysInvtCommentLine."Document Type"::Recording);
               PhysInvtCommentLine.SETRANGE("Order No.","Order No.");
               PhysInvtCommentLine.SETRANGE("Recording No.","Recording No.");
               PhysInvtCommentLine.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(CannotRenameErr,TABLECAPTION);
             END;

    CaptionML=ENU=Phys. Invt. Record Header;
    LookupPageID=Page5880;
    DrillDownPageID=Page5880;
  }
  FIELDS
  {
    { 1   ;   ;Order No.           ;Code20        ;TableRelation="Phys. Invt. Order Header";
                                                   OnValidate=BEGIN
                                                                IF (xRec."Order No." <> '') AND ("Order No." <> xRec."Order No.") THEN
                                                                  ERROR(CannotChangeErr,FIELDCAPTION("Order No."));
                                                              END;

                                                   CaptionML=ENU=Order No. }
    { 2   ;   ;Recording No.       ;Integer       ;CaptionML=ENU=Recording No.;
                                                   Editable=No }
    { 10  ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 11  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Finished;
                                                   OptionString=Open,Finished;
                                                   Editable=No }
    { 30  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Phys. Invt. Comment Line" WHERE (Document Type=CONST(Recording),
                                                                                                       Order No.=FIELD(Order No.),
                                                                                                       Recording No.=FIELD(Recording No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 31  ;   ;Person Responsible  ;Code20        ;TableRelation=Employee;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Person Responsible }
    { 40  ;   ;Allow Recording Without Order;Boolean;
                                                   CaptionML=ENU=Allow Recording Without Order }
    { 100 ;   ;Date Recorded       ;Date          ;CaptionML=ENU=Date Recorded }
    { 101 ;   ;Time Recorded       ;Time          ;CaptionML=ENU=Time Recorded }
    { 102 ;   ;Person Recorded     ;Code20        ;TableRelation=Employee;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Person Recorded }
    { 110 ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=ENU=Location Code }
    { 111 ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF "Bin Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Location Code");
                                                                  Location.GET("Location Code");
                                                                  Location.TESTFIELD("Bin Mandatory",TRUE);
                                                                  Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Bin Code }
  }
  KEYS
  {
    {    ;Order No.,Recording No.                 ;Clustered=Yes }
    {    ;Status                                   }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Order No.,Recording No.,Description      }
  }
  CODE
  {
    VAR
      CannotRenameErr@1000 : TextConst '@@@="%1 = Table caption";ENU=You cannot rename a %1.';
      CannotChangeErr@1001 : TextConst '@@@="%1 = Field caption";ENU=You cannot change the %1.';
      PhysInvtRecordHeader@1002 : Record 5877;
      PhysInvtRecordLine@1003 : Record 5878;
      PhysInvtOrderHeader@1004 : Record 5875;
      PhysInvtCommentLine@1005 : Record 5883;
      Location@1006 : Record 14;

    BEGIN
    END.
  }
}

