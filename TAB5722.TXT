OBJECT Table 5722 Item Category
{
  OBJECT-PROPERTIES
  {
    Date=03/26/20;
    Time=12:00:00 PM;
    Version List=NAVW114.12;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
               UpdateIndentation;
               ItemCategoryManagement.CalcPresentationOrder(Rec);
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               UpdateIndentation;
               ItemCategoryManagement.CalcPresentationOrder(Rec);
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnDelete=BEGIN
               IF HasChildren THEN
                 ERROR(DeleteWithChildrenErr);
               UpdateDeletedCategoryItems;
               DeleteAssignedAttributes;
             END;

    OnRename=BEGIN
               "Presentation Order" := 0;
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    CaptionML=ENU=Item Category;
    LookupPageID=Page5730;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Parent Category     ;Code20        ;TableRelation="Item Category";
                                                   OnValidate=VAR
                                                                ItemCategory@1000 : Record 5722;
                                                                ItemAttributeManagement@1002 : Codeunit 7500;
                                                                ParentCategory@1001 : Code[20];
                                                              BEGIN
                                                                ParentCategory := "Parent Category";
                                                                WHILE ItemCategory.GET(ParentCategory) DO BEGIN
                                                                  IF ItemCategory.Code = Code THEN
                                                                    ERROR(CyclicInheritanceErr);
                                                                  ParentCategory := ItemCategory."Parent Category";
                                                                END;
                                                                IF "Parent Category" <> xRec."Parent Category" THEN
                                                                  ItemAttributeManagement.UpdateCategoryAttributesAfterChangingParentCategory(Code,"Parent Category",xRec."Parent Category");
                                                              END;

                                                   CaptionML=ENU=Parent Category }
    { 3   ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 9   ;   ;Indentation         ;Integer       ;CaptionML=ENU=Indentation;
                                                   MinValue=0 }
    { 10  ;   ;Presentation Order  ;Integer       ;CaptionML=ENU=Presentation Order }
    { 11  ;   ;Has Children        ;Boolean       ;CaptionML=ENU=Has Children }
    { 12  ;   ;Last Modified Date Time;DateTime   ;CaptionML=ENU=Last Modified Date Time;
                                                   Editable=No }
    { 8000;   ;Id                  ;GUID          ;CaptionML=ENU=Id }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Parent Category                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CyclicInheritanceErr@1001 : TextConst 'ENU=An item category cannot be a parent of itself or any of its children.';
      ItemCategoryManagement@1002 : Codeunit 5722;
      DeleteWithChildrenErr@1003 : TextConst 'ENU=You cannot delete this item category because it has child item categories.';
      DeleteItemInheritedAttributesQst@1004 : TextConst '@@@=%1 - item category code;ENU="One or more items belong to item category ''''%1''''.\\Do you want to delete the inherited item attributes for the items in question? "';

    [External]
    PROCEDURE HasChildren@1() : Boolean;
    VAR
      ItemCategory@1000 : Record 5722;
    BEGIN
      ItemCategory.SETRANGE("Parent Category",Code);
      EXIT(NOT ItemCategory.ISEMPTY)
    END;

    [External]
    PROCEDURE GetStyleText@3() : Text;
    BEGIN
      IF Indentation = 0 THEN
        EXIT('Strong');

      IF HasChildren THEN
        EXIT('Strong');

      EXIT('');
    END;

    LOCAL PROCEDURE UpdateDeletedCategoryItems@6();
    VAR
      CategoryItem@1001 : Record 27;
      TempCategoryItemAttributeValue@1000 : TEMPORARY Record 7501;
      ItemAttributeManagement@1003 : Codeunit 7500;
      DeleteItemInheritedAttributes@1002 : Boolean;
    BEGIN
      CategoryItem.SETRANGE("Item Category Code",Code);
      IF CategoryItem.ISEMPTY THEN
        EXIT;
      DeleteItemInheritedAttributes := CONFIRM(STRSUBSTNO(DeleteItemInheritedAttributesQst,Code));
      IF DeleteItemInheritedAttributes THEN
        TempCategoryItemAttributeValue.LoadCategoryAttributesFactBoxData(Code);
      IF CategoryItem.FIND('-') THEN
        REPEAT
          CategoryItem.VALIDATE("Item Category Code",'');
          CategoryItem.MODIFY;
          IF DeleteItemInheritedAttributes THEN
            ItemAttributeManagement.DeleteItemAttributeValueMapping(CategoryItem,TempCategoryItemAttributeValue);
        UNTIL CategoryItem.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteAssignedAttributes@4();
    VAR
      ItemAttributeValueMapping@1000 : Record 7505;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
      ItemAttributeValueMapping.SETRANGE("No.",Code);
      ItemAttributeValueMapping.DELETEALL;
    END;

    LOCAL PROCEDURE UpdateIndentation@5();
    VAR
      ParentItemCategory@1000 : Record 5722;
    BEGIN
      IF ParentItemCategory.GET("Parent Category") THEN
        UpdateIndentationTree(ParentItemCategory.Indentation + 1)
      ELSE
        UpdateIndentationTree(0);
    END;

    PROCEDURE UpdateIndentationTree@7(Level@1000 : Integer);
    VAR
      ItemCategory@1001 : Record 5722;
    BEGIN
      Indentation := Level;

      ItemCategory.SETRANGE("Parent Category",Code);
      IF ItemCategory.FINDSET THEN
        REPEAT
          ItemCategory.UpdateIndentationTree(Level + 1);
          ItemCategory.MODIFY;
        UNTIL ItemCategory.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

