OBJECT Codeunit 3 G/L Account-Indent
{
  OBJECT-PROPERTIES
  {
    Date=03/24/19;
    Time=12:00:00 PM;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            ConfirmManagement@1000 : Codeunit 27;
          BEGIN
            IF NOT ConfirmManagement.ConfirmProcess(GLAccIndentQst,TRUE) THEN
              EXIT;

            Indent;
          END;

  }
  CODE
  {
    VAR
      GLAccIndentQst@1011 : TextConst 'ENU=This function updates the indentation of all the G/L accounts in the chart of accounts. All accounts between a Begin-Total and the matching End-Total are indented one level. The Totaling for each End-total is also updated.\\Do you want to indent the chart of accounts?';
      ICAccIndentQst@1012 : TextConst 'ENU=This function updates the indentation of all the G/L accounts in the chart of accounts. All accounts between a Begin-Total and the matching End-Total are indented one level. \\Do you want to indent the chart of accounts?';
      Text004@1004 : TextConst 'ENU=Indenting the Chart of Accounts #1##########';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.';
      ArrayExceededErr@1006 : TextConst '@@@="%1 = A number bigger than 1";ENU=You can only indent %1 levels for accounts of the type Begin-Total.';
      GLAcc@1007 : Record 15;
      Window@1008 : Dialog;
      AccNo@1009 : ARRAY [10] OF Code[20];
      i@1010 : Integer;

    [External]
    PROCEDURE Indent@1();
    BEGIN
      Window.OPEN(Text004);

      WITH GLAcc DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"No.");

            IF "Account Type" = "Account Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "No.");
              IF Totaling = '' THEN
                Totaling := AccNo[i] + '..' + "No.";
              i := i - 1;
            END;

            VALIDATE(Indentation,i);
            MODIFY;

            IF "Account Type" = "Account Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              IF i > ARRAYLEN(AccNo) THEN
                ERROR(ArrayExceededErr,ARRAYLEN(AccNo));
              AccNo[i] := "No.";
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;

      OnAfterIndent;
    END;

    [External]
    PROCEDURE RunICAccountIndent@4();
    VAR
      ConfirmManagement@1000 : Codeunit 27;
    BEGIN
      IF NOT ConfirmManagement.ConfirmProcess(ICAccIndentQst,TRUE) THEN
        EXIT;

      IndentICAccount;
    END;

    LOCAL PROCEDURE IndentICAccount@2();
    VAR
      ICGLAcc@1000 : Record 410;
    BEGIN
      Window.OPEN(Text004);
      WITH ICGLAcc DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"No.");

            IF "Account Type" = "Account Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "No.");
              i := i - 1;
            END;

            VALIDATE(Indentation,i);
            MODIFY;

            IF "Account Type" = "Account Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              AccNo[i] := "No.";
            END;
          UNTIL NEXT = 0;
      Window.CLOSE;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterIndent@3();
    BEGIN
    END;

    BEGIN
    END.
  }
}

