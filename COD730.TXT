OBJECT Codeunit 730 Copy Item
{
  OBJECT-PROPERTIES
  {
    Date=05/22/19;
    Time=12:00:00 PM;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    TableNo=27;
    OnRun=VAR
            CopyItem@1003 : Report 730;
            FirstItemNo@1001 : Code[20];
            LastItemNo@1000 : Code[20];
          BEGIN
            CopyItem.SetItem(Rec);
            CopyItem.RUNMODAL;
            IF CopyItem.IsItemCopied(FirstItemNo,LastItemNo) THEN
              ShowNotification(Rec,FirstItemNo,LastItemNo);
          END;

  }
  CODE
  {
    VAR
      ItemCopiedMsg@1000 : TextConst '@@@=%1 - item number;ENU=Item %1 was successfully copied.';
      ShowCreatedItemTxt@1002 : TextConst 'ENU=Show created item.';
      ShowCreatedItemsTxt@1001 : TextConst 'ENU=Show created items.';

    LOCAL PROCEDURE ShowNotification@7(Item@1002 : Record 27;FirstItemNo@1001 : Code[20];LastItemNo@1000 : Code[20]);
    VAR
      NotificationLifecycleMgt@1004 : Codeunit 1511;
      ItemCopiedNotification@1003 : Notification;
      ShowCreatedActionCaption@1007 : Text;
    BEGIN
      ItemCopiedNotification.ID := CREATEGUID;
      ItemCopiedNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
      ItemCopiedNotification.SETDATA('FirstItemNo',FirstItemNo);
      ItemCopiedNotification.SETDATA('LastItemNo',LastItemNo);
      ItemCopiedNotification.MESSAGE(STRSUBSTNO(ItemCopiedMsg,Item."No."));
      IF FirstItemNo = LastItemNo THEN
        ShowCreatedActionCaption := ShowCreatedItemTxt
      ELSE
        ShowCreatedActionCaption := ShowCreatedItemsTxt;
      ItemCopiedNotification.ADDACTION(ShowCreatedActionCaption,CODEUNIT::"Copy Item",'ShowCreatedItems');
      NotificationLifecycleMgt.SendNotification(ItemCopiedNotification,Item.RECORDID);
    END;

    [External]
    PROCEDURE ShowCreatedItems@9(VAR ItemCopiedNotification@1000 : Notification);
    VAR
      Item@1001 : Record 27;
    BEGIN
      Item.SETRANGE(
        "No.",
        ItemCopiedNotification.GETDATA('FirstItemNo'),
        ItemCopiedNotification.GETDATA('LastItemNo'));
      IF Item.FINDFIRST THEN
        IF Item.COUNT = 1 THEN
          PAGE.RUNMODAL(PAGE::"Item Card",Item)
        ELSE
          PAGE.RUNMODAL(PAGE::"Item List",Item);
    END;

    BEGIN
    END.
  }
}

