OBJECT Page 737 VAT Return Period List
{
  OBJECT-PROPERTIES
  {
    Date=05/22/19;
    Time=12:00:00 PM;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=ENU=Tax Return Periods;
    ApplicationArea=#Basic,#Suite;
    SourceTable=Table737;
    PageType=List;
    UsageCategory=ReportsAndAnalysis;
    CardPageID=VAT Return Period Card;
    OnOpenPage=BEGIN
                 VATReportSetup.GET;
                 IsEditable := VATReportSetup."Manual Receive Period CU ID" = 0;
               END;

    OnAfterGetRecord=BEGIN
                       InitPageControllers;
                     END;

    OnInsertRecord=BEGIN
                     IF NOT IsEditable THEN
                       ERROR('');
                   END;

    OnAfterGetCurrRecord=BEGIN
                           InitPageControllers;
                         END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 11      ;1   ;Action    ;
                      Name=Get VAT Return Periods;
                      CaptionML=ENU=Get Tax Return Periods;
                      ToolTipML=ENU=Load the Tax return periods that are set up in the system.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=NOT IsEditable;
                      PromotedIsBig=Yes;
                      Image=GetLines;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 VATReportMgt@1000 : Codeunit 737;
                               BEGIN
                                 VATReportMgt.GetVATReturnPeriods;
                               END;
                                }
      { 12      ;1   ;Action    ;
                      Name=Create VAT Return;
                      CaptionML=ENU=Create Tax Return;
                      ToolTipML=ENU=Create a new Tax return from the selected Tax return period.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=CreateVATReturnEnabled;
                      PromotedIsBig=Yes;
                      Image=RefreshLines;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 VATReturnPeriod@1000 : Record 737;
                                 VATReportMgt@1001 : Codeunit 737;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(VATReturnPeriod);
                                 IF VATReturnPeriod.COUNT = 1 THEN
                                   VATReportMgt.CreateVATReturnFromVATPeriod(Rec);
                               END;
                                }
      { 13      ;0   ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 14      ;1   ;Action    ;
                      Name=Open VAT Return Card;
                      CaptionML=ENU=Open Tax Return Card;
                      ToolTipML=ENU=Open the Tax return card for the selected Tax return period.;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=OpenVATReturnEnabled;
                      Image=ShowList;
                      OnAction=VAR
                                 VATReturnPeriod@1000 : Record 737;
                                 VATReportMgt@1001 : Codeunit 737;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(VATReturnPeriod);
                                 IF VATReturnPeriod.COUNT = 1 THEN
                                   VATReportMgt.OpenVATReturnCardFromVATPeriod(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Editable=IsEditable;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the start date of the Tax return period.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Start Date" }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the end date of the Tax return period.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="End Date" }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the due date for the Tax return period.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due Date";
                StyleExpr=WarningStyleExpr }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the status of the Tax return period.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Status;
                StyleExpr=WarningStyleExpr }

    { 7   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the Tax return period received date.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Received Date" }

    { 15  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the associated Tax return.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Return No.";
                OnDrillDown=BEGIN
                              DrillDownVATReturn;
                            END;
                             }

    { 16  ;2   ;Field     ;
                Name=VATReturnStatus;
                CaptionML=ENU=Tax Return Status;
                ToolTipML=ENU=Specifies the status of the associated Tax return.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VATReturnStatus;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownVATReturn;
                            END;
                             }

    { 8   ;    ;Container ;
                ContainerType=FactBoxArea }

    { 9   ;1   ;Part      ;
                ApplicationArea=#Basic,#Suite;
                SubPageLink=No.=FIELD(No.);
                PagePartID=Page736;
                PartType=Page }

  }
  CODE
  {
    VAR
      VATReportSetup@1000 : Record 743;
      WarningStyleExpr@1001 : Text;
      CreateVATReturnEnabled@1002 : Boolean;
      OpenVATReturnEnabled@1005 : Boolean;
      IsEditable@1003 : Boolean;
      VATReturnStatus@1006 : ' ,Open,Released,Submitted,Accepted,Closed,Rejected,Canceled';

    LOCAL PROCEDURE InitPageControllers@2();
    BEGIN
      CreateVATReturnEnabled := (Status = Status::Open) AND ("VAT Return No." = '');
      OpenVATReturnEnabled := (Status = Status::Open) OR ("VAT Return No." <> '');
      CALCFIELDS("VAT Return Status");
      IF "VAT Return No." <> '' THEN
        VATReturnStatus := "VAT Return Status" + 1
      ELSE
        VATReturnStatus := VATReturnStatus::" ";
      CheckOpenOrOverdue;
    END;

    LOCAL PROCEDURE CheckOpenOrOverdue@1();
    BEGIN
      IF (Status = Status::Open) AND ("Due Date" <> 0D) AND
         (("Due Date" < WORKDATE) OR
          VATReportSetup.IsPeriodReminderCalculation AND
          ("Due Date" >= WORKDATE) AND ("Due Date" <= CALCDATE(VATReportSetup."Period Reminder Calculation",WORKDATE)))
      THEN
        WarningStyleExpr := 'Unfavorable'
      ELSE
        WarningStyleExpr := '';
    END;

    BEGIN
    END.
  }
}

