OBJECT Page 5733 Item Category Card
{
  OBJECT-PROPERTIES
  {
    Date=03/26/20;
    Time=12:00:00 PM;
    Version List=NAVW114.12;
  }
  PROPERTIES
  {
    CaptionML=ENU=Item Category Card;
    DeleteAllowed=No;
    SourceTable=Table5722;
    PageType=Card;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 IF Code <> '' THEN
                   CurrPage.Attributes.PAGE.LoadAttributes(Code);
               END;

    OnAfterGetRecord=BEGIN
                       IF Code <> '' THEN
                         CurrPage.Attributes.PAGE.LoadAttributes(Code);

                       CanDelete := NOT HasChildren;
                     END;

    OnInsertRecord=BEGIN
                     CurrPage.Attributes.PAGE.SetItemCategoryCode(Code);
                   END;

    OnQueryClosePage=BEGIN
                       IF Code <> '' THEN
                         CurrPage.Attributes.PAGE.SaveAttributes(Code);

                       ItemCategoryManagement.CheckPresentationOrder;
                     END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 7       ;1   ;Action    ;
                      Name=Delete;
                      CaptionML=ENU=Delete;
                      ToolTipML=ENU=Delete the record.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=CanDelete;
                      PromotedIsBig=Yes;
                      Image=Delete;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 IF CONFIRM(STRSUBSTNO(DeleteQst,Code)) THEN
                                   DELETE(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the item category.;
                ApplicationArea=#Basic,#Suite;
                NotBlank=Yes;
                SourceExpr=Code;
                OnValidate=BEGIN
                             IF (xRec.Code <> '') AND (xRec.Code <> Code) THEN
                               CurrPage.Attributes.PAGE.SaveAttributes(Code);
                           END;
                            }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Specifies a description of the item category.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description }

    { 5   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the item category that this item category belongs to. Item attributes that are assigned to a parent item category also apply to the child item category.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Parent Category";
                OnValidate=BEGIN
                             IF (Code <> '') AND ("Parent Category" <> xRec."Parent Category") THEN
                               PersistCategoryAttributes;
                           END;
                            }

    { 12  ;1   ;Part      ;
                Name=Attributes;
                CaptionML=ENU=Attributes;
                ApplicationArea=#Basic,#Suite;
                PagePartID=Page5734;
                PartType=Page;
                ShowFilter=No }

  }
  CODE
  {
    VAR
      ItemCategoryManagement@1000 : Codeunit 5722;
      DeleteQst@1001 : TextConst '@@@=%1 - item category name;ENU=Delete %1?';
      CanDelete@1002 : Boolean;

    LOCAL PROCEDURE PersistCategoryAttributes@1();
    BEGIN
      CurrPage.Attributes.PAGE.SaveAttributes(Code);
      CurrPage.Attributes.PAGE.LoadAttributes(Code);
      CurrPage.UPDATE(TRUE);
    END;

    BEGIN
    END.
  }
}

