OBJECT Report 357 Copy Company
{
  OBJECT-PROPERTIES
  {
    Date=05/22/19;
    Time=12:00:00 PM;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Company;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table2000000006;
               DataItemTableView=SORTING(Name);
               OnAfterGetRecord=BEGIN
                                  ProgressWindow.OPEN(STRSUBSTNO(ProgressMsg,NewCompanyName));

                                  IF BreakReport THEN
                                    CurrReport.BREAK;
                                  COPYCOMPANY(Name,NewCompanyName);
                                  BreakReport := TRUE;
                                END;

               OnPostDataItem=BEGIN
                                ProgressWindow.CLOSE;
                                SetNewNameToNewCompanyInfo;
                                SetRecurringJobsOnHold;
                                MESSAGE(CopySuccessMsg,Name);
                              END;
                               }

    { 2   ;1   ;DataItem;                    ;
               DataItemTable=Table9176;
               DataItemTableView=SORTING(Company Name);
               OnAfterGetRecord=VAR
                                  ExperienceTierSetup@1001 : Record 9176;
                                  ApplicationAreaMgmt@1000 : Codeunit 9178;
                                BEGIN
                                  ExperienceTierSetup := "Experience Tier Setup";
                                  ExperienceTierSetup."Company Name" := NewCompanyName;
                                  IF ExperienceTierSetup.INSERT THEN ;
                                  ApplicationAreaMgmt.SetExperienceTierOtherCompany(ExperienceTierSetup,NewCompanyName);
                                END;

               DataItemLink=Company Name=FIELD(Name) }

    { 3   ;1   ;DataItem;                    ;
               DataItemTable=Table9651;
               DataItemTableView=SORTING(Report ID,Company Name);
               OnAfterGetRecord=VAR
                                  ReportLayoutSelection@1000 : Record 9651;
                                BEGIN
                                  ReportLayoutSelection := "Report Layout Selection";
                                  ReportLayoutSelection."Report ID" := "Report ID";
                                  ReportLayoutSelection."Company Name" := NewCompanyName;
                                  IF ReportLayoutSelection.INSERT THEN;
                                END;

               DataItemLink=Company Name=FIELD(Name) }

    { 4   ;1   ;DataItem;                    ;
               DataItemTable=Table9650;
               DataItemTableView=SORTING(Report ID,Company Name,Type);
               OnAfterGetRecord=VAR
                                  CustomReportLayout@1000 : Record 9650;
                                BEGIN
                                  CustomReportLayout := "Custom Report Layout";
                                  CustomReportLayout.Code := '';
                                  CustomReportLayout."Company Name" := NewCompanyName;
                                  IF CustomReportLayout.INSERT(TRUE) THEN;
                                END;

               DataItemLink=Company Name=FIELD(Name) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      ShowFilter=No;
    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  GroupType=Group }

      { 3   ;2   ;Field     ;
                  Name=New Company Name;
                  CaptionML=ENU=New Company Name;
                  ToolTipML=ENU=Specifies the name of the new company. The name can have a maximum of 30 characters. If the database collation is case-sensitive, you can have one company called COMPANY and another called Company. However, if the database is case-insensitive, you cannot create companies with names that differ only by case.;
                  ApplicationArea=#Basic,#Suite;
                  NotBlank=Yes;
                  SourceExpr=NewCompanyName;
                  OnValidate=BEGIN
                               NewCompanyName := DELCHR(NewCompanyName,'<>');
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ProgressWindow@1009 : Dialog;
      BreakReport@1001 : Boolean;
      NewCompanyName@1000 : Text[30];
      ProgressMsg@1010 : TextConst '@@@=Creating new company Contoso Corporation.;ENU=Creating new company %1.';
      CopySuccessMsg@1002 : TextConst '@@@=Company CRONUS International Ltd. has been copied successfully.;ENU=Company %1 has been copied successfully.';

    [External]
    PROCEDURE GetCompanyName@6() : Text[30];
    BEGIN
      EXIT(NewCompanyName);
    END;

    LOCAL PROCEDURE SetNewNameToNewCompanyInfo@3();
    VAR
      CompanyInformation@1001 : Record 79;
      Company@1002 : Record 2000000006;
    BEGIN
      IF Company.GET(NewCompanyName) THEN;
      Company."Display Name" := NewCompanyName;
      Company.MODIFY;

      IF CompanyInformation.CHANGECOMPANY(NewCompanyName) THEN
        IF CompanyInformation.GET THEN BEGIN
          CompanyInformation.Name := NewCompanyName;
          CompanyInformation.MODIFY(TRUE);
        END;

      OnAfterSetNewNameToNewCompanyInfo(NewCompanyName);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetNewNameToNewCompanyInfo@11(NewCompanyName@1000 : Text[30]);
    BEGIN
    END;

    LOCAL PROCEDURE SetRecurringJobsOnHold@1();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      JobQueueEntry.CHANGECOMPANY(NewCompanyName);

      JobQueueEntry.SETRANGE("Recurring Job",TRUE);
      JobQueueEntry.SETRANGE(Status,JobQueueEntry.Status::Ready);
      IF JobQueueEntry.FINDSET(TRUE) THEN
        REPEAT
          JobQueueEntry.Status := JobQueueEntry.Status::"On Hold";
          JobQueueEntry.MODIFY;
        UNTIL JobQueueEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

