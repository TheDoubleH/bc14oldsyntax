OBJECT Codeunit 27032 Update CFDI Fields Sales Doc
{
  OBJECT-PROPERTIES
  {
    Date=07/23/19;
    Time=12:00:00 PM;
    Version List=NAVNA14.03;
  }
  PROPERTIES
  {
    Permissions=TableData 112=rm,
                TableData 114=rm,
                TableData 5992=rm,
                TableData 5994=rm;
    OnRun=VAR
            UpgradeTagMgt@1310001 : Codeunit 9999;
            UpgradeTags@1310000 : Codeunit 9998;
          BEGIN
            IF UpgradeTagMgt.HasUpgradeTag(UpgradeTags.GetCFDIPurposeRelationFieldsDocUpdateTag) THEN
              EXIT;

            UpdateSalesDocuments;
            UpdateServiceDocuments;

            UpgradeTagMgt.SetUpgradeTag(UpgradeTags.GetCFDIPurposeRelationFieldsDocUpdateTag);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE UpdateSalesDocuments@1310001();
    VAR
      Customer@1310000 : Record 18;
      SalesHeader@1310001 : Record 36;
      SalesInvoiceHeader@1310002 : Record 112;
      SalesCrMemoHeader@1310003 : Record 114;
    BEGIN
      Customer.SETFILTER("CFDI Purpose",'<>%1','');
      IF Customer.ISEMPTY THEN
        EXIT;

      Customer.FINDSET;
      REPEAT
        SalesHeader.SETRANGE("Bill-to Customer No.",Customer."No.");
        IF NOT SalesHeader.ISEMPTY THEN BEGIN
          SalesHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
          SalesHeader.MODIFYALL("CFDI Relation",Customer."CFDI Relation");
        END;

        SalesHeader.SETRANGE("Bill-to Customer No.",'');
        SalesHeader.SETRANGE("Sell-to Customer No.",Customer."No.");
        IF NOT SalesHeader.ISEMPTY THEN BEGIN
          SalesHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
          SalesHeader.MODIFYALL("CFDI Relation",Customer."CFDI Relation");
        END;

        SalesInvoiceHeader.SETFILTER(
          "Electronic Document Status",'%1|%2|%3',
          SalesInvoiceHeader."Electronic Document Status"::" ",
          SalesInvoiceHeader."Electronic Document Status"::"Stamp Request Error",
          SalesInvoiceHeader."Electronic Document Status"::"Cancel Error");
        SalesInvoiceHeader.SETRANGE("Bill-to Customer No.",Customer."No.");
        IF NOT SalesInvoiceHeader.ISEMPTY THEN
          SalesInvoiceHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
        SalesInvoiceHeader.SETRANGE("Bill-to Customer No.",'');
        SalesInvoiceHeader.SETRANGE("Sell-to Customer No.",Customer."No.");
        IF NOT SalesInvoiceHeader.ISEMPTY THEN
          SalesInvoiceHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");

        SalesCrMemoHeader.SETFILTER(
          "Electronic Document Status",'%1|%2|%3',
          SalesCrMemoHeader."Electronic Document Status"::" ",
          SalesCrMemoHeader."Electronic Document Status"::"Stamp Request Error",
          SalesCrMemoHeader."Electronic Document Status"::"Cancel Error");
        SalesCrMemoHeader.SETRANGE("Bill-to Customer No.",Customer."No.");
        IF NOT SalesCrMemoHeader.ISEMPTY THEN BEGIN
          SalesCrMemoHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
          SalesCrMemoHeader.MODIFYALL("CFDI Relation",Customer."CFDI Relation");
        END;
        SalesCrMemoHeader.SETRANGE("Bill-to Customer No.",'');
        SalesCrMemoHeader.SETRANGE("Sell-to Customer No.",Customer."No.");
        IF NOT SalesCrMemoHeader.ISEMPTY THEN BEGIN
          SalesCrMemoHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
          SalesCrMemoHeader.MODIFYALL("CFDI Relation",Customer."CFDI Relation");
        END;
      UNTIL Customer.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateServiceDocuments@1310016();
    VAR
      Customer@1310000 : Record 18;
      ServiceHeader@1310001 : Record 5900;
      ServiceInvoiceHeader@1310002 : Record 5992;
      ServiceCrMemoHeader@1310003 : Record 5994;
    BEGIN
      Customer.SETFILTER("CFDI Purpose",'<>%1','');
      IF Customer.ISEMPTY THEN
        EXIT;

      Customer.FINDSET;
      REPEAT
        ServiceHeader.SETRANGE("Bill-to Customer No.",Customer."No.");
        IF NOT ServiceHeader.ISEMPTY THEN BEGIN
          ServiceHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
          ServiceHeader.MODIFYALL("CFDI Relation",Customer."CFDI Relation");
        END;

        ServiceInvoiceHeader.SETFILTER(
          "Electronic Document Status",'%1|%2|%3',
          ServiceInvoiceHeader."Electronic Document Status"::" ",
          ServiceInvoiceHeader."Electronic Document Status"::"Stamp Request Error",
          ServiceInvoiceHeader."Electronic Document Status"::"Cancel Error");
        ServiceInvoiceHeader.SETRANGE("Bill-to Customer No.",Customer."No.");
        IF NOT ServiceInvoiceHeader.ISEMPTY THEN
          ServiceInvoiceHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");

        ServiceCrMemoHeader.SETFILTER(
          "Electronic Document Status",'%1|%2|%3',
          ServiceCrMemoHeader."Electronic Document Status"::" ",
          ServiceCrMemoHeader."Electronic Document Status"::"Stamp Request Error",
          ServiceCrMemoHeader."Electronic Document Status"::"Cancel Error");
        ServiceCrMemoHeader.SETRANGE("Bill-to Customer No.",Customer."No.");
        IF NOT ServiceCrMemoHeader.ISEMPTY THEN BEGIN
          ServiceCrMemoHeader.MODIFYALL("CFDI Purpose",Customer."CFDI Purpose");
          ServiceCrMemoHeader.MODIFYALL("CFDI Relation",Customer."CFDI Relation");
        END;
      UNTIL Customer.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

