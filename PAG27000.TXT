OBJECT Page 27000 Export Electr. Accounting
{
  OBJECT-PROPERTIES
  {
    Date=03/24/19;
    Time=12:00:00 PM;
    Version List=NAVNA14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Export Electr. Accounting;
    ApplicationArea=#Basic,#Suite;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    UsageCategory=Tasks;
    PromotedActionCategoriesML=ENU=New,Process,Report,Export;
    ShowFilter=No;
    OnOpenPage=BEGIN
                 Year := DATE2DMY(WORKDATE,3);
                 Month := DATE2DMY(WORKDATE,2);
                 EnableControls;
               END;

    ActionList=ACTIONS
    {
      { 1       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Name=Export;
                      CaptionML=ENU=Export...;
                      ToolTipML=ENU=Export the specified accounting information.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ExportFile;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 CASE ExportType OF
                                   ExportType::"Chart of Accounts":
                                     ExportAccounts.ExportChartOfAccounts(Year,Month);
                                   ExportType::"Trial Balance":
                                     ExportAccounts.ExportBalanceSheet(Year,Month,DeliveryType,UpdateDate,ClosingBalanceSheet);
                                   ExportType::Transactions:
                                     ExportAccounts.ExportTransactions(Year,Month,RequestType,OrderNumber,ProcessNumber);
                                   ExportType::"Auxiliary Accounts":
                                     ExportAccounts.ExportAuxiliaryAccounts(Year,Month,RequestType,OrderNumber,ProcessNumber);
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 5   ;    ;Container ;
                Name=Content;
                ContainerType=ContentArea }

    { 1020005;1;Field     ;
                Name=ExportType;
                CaptionML=ENU=Export Type;
                ToolTipML=ENU=Specifies which accounting information is exported, such as the trial balance or specific transactions.;
                OptionCaptionML=ENU=Chart of Accounts,Trial Balance,Transactions,Auxiliary Accounts;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ExportType;
                OnValidate=BEGIN
                             EnableControls;
                           END;
                            }

    { 6   ;1   ;Field     ;
                Name=Year;
                CaptionML=ENU=Year;
                ToolTipML=ENU="Specifies for which year accounting information is exported. ";
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Year;
                MinValue=2000;
                MaxValue=2099;
                ShowMandatory=TRUE }

    { 7   ;1   ;Field     ;
                Name=Month;
                CaptionML=ENU=Month;
                ToolTipML=ENU="Specifies for which month accounting information is exported. ";
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Month;
                MinValue=1;
                MaxValue=12;
                Enabled=NOT ClosingBalanceSheet;
                ShowMandatory=TRUE }

    { 1020003;1;Field     ;
                Name=DeliveryType;
                CaptionML=ENU=Delivery Type;
                ToolTipML=ENU="Specifies if the exported accounting information will complement existing information. ";
                OptionCaptionML=ENU=Normal,Complementary;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DeliveryType;
                Enabled=EnableDeliveryType;
                OnValidate=BEGIN
                             EnableControls;
                           END;
                            }

    { 1020004;1;Field     ;
                Name=UpdateDate;
                CaptionML=ENU=Update Date;
                ToolTipML=ENU=Specifies when the accounting information was last exported.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=UpdateDate;
                Enabled=EnableUpdateDate;
                ShowMandatory=TRUE }

    { 1020000;1;Field     ;
                Name=RequestType;
                CaptionML=ENU=Request Type;
                ToolTipML=ENU=Specifies the request type for the exported information.;
                OptionCaptionML=ENU=AF,FC,DE,CO;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=RequestType;
                Enabled=EnableRequestType;
                OnValidate=BEGIN
                             EnableControls;
                           END;
                            }

    { 1020006;1;Field     ;
                Name=OrderNumber;
                CaptionML=ENU=Order Number;
                ToolTipML=ENU=Specifies the order number that will be assigned to the export.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=OrderNumber;
                Enabled=EnableOrderNumber;
                ShowMandatory=TRUE }

    { 1020007;1;Field     ;
                Name=ProcessNumber;
                CaptionML=ENU=Process Number;
                ToolTipML=ENU=Specifies the process number that will be assigned to the export.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ProcessNumber;
                Enabled=EnableProcessNumber;
                ShowMandatory=TRUE }

    { 1020008;1;Field     ;
                Name=ClosingBalanceSheet;
                CaptionML=ENU=Closing Balance Sheet;
                ToolTipML=ENU=Specifies if the exported trial balance will include closing balances.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ClosingBalanceSheet;
                Enabled=EnableClosingBalanceSheet }

  }
  CODE
  {
    VAR
      ExportAccounts@1020000 : Codeunit 27000;
      ExportType@1020007 : 'Chart of Accounts,Trial Balance,Transactions,Auxiliary Accounts';
      Month@1020001 : Integer;
      Year@1020002 : Integer;
      DeliveryType@1020005 : 'Normal,Complementary';
      UpdateDate@1020006 : Date;
      RequestType@1020008 : 'AF,FC,DE,CO';
      OrderNumber@1020009 : Text[13];
      ProcessNumber@1020010 : Text[14];
      ClosingBalanceSheet@1020011 : Boolean;
      EnableUpdateDate@1020012 : Boolean;
      EnableDeliveryType@1020013 : Boolean;
      EnableRequestType@1020016 : Boolean;
      EnableOrderNumber@1020014 : Boolean;
      EnableProcessNumber@1020015 : Boolean;
      EnableClosingBalanceSheet@1020003 : Boolean;

    LOCAL PROCEDURE EnableControls@1020011();
    BEGIN
      ResetControls;

      CASE ExportType OF
        ExportType::"Trial Balance":
          BEGIN
            EnableDeliveryType := TRUE;
            EnableClosingBalanceSheet := TRUE;
            IF DeliveryType = DeliveryType::Complementary THEN BEGIN
              EnableUpdateDate := TRUE;
              UpdateDate := WORKDATE;
            END;
          END;
        ExportType::Transactions,
        ExportType::"Auxiliary Accounts":
          BEGIN
            EnableRequestType := TRUE;
            IF RequestType IN [RequestType::AF,RequestType::FC] THEN
              EnableOrderNumber := TRUE
            ELSE
              EnableProcessNumber := TRUE;
          END;
      END;
    END;

    LOCAL PROCEDURE ResetControls@1020019();
    BEGIN
      EnableDeliveryType := FALSE;
      EnableUpdateDate := FALSE;
      EnableRequestType := FALSE;
      EnableOrderNumber := FALSE;
      EnableProcessNumber := FALSE;
      EnableClosingBalanceSheet := FALSE;

      UpdateDate := 0D;
    END;

    BEGIN
    END.
  }
}

