OBJECT Table 7505 Item Attribute Value Mapping
{
  OBJECT-PROPERTIES
  {
    Date=06/07/21;
    Time=12:00:00 PM;
    Version List=NAVW114.25;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ItemAttributeValue@1003 : Record 7501;
               RecRef@1000 : RecordRef;
               FieldRef@1002 : FieldRef;
             BEGIN
               RecRef.OPEN("Table ID");
               FieldRef := RecRef.FIELD(1);
               FieldRef.SETRANGE("No.");
               RecRef.FINDFIRST;

               IF "Item Attribute Value ID" <> 0 THEN
                 ItemAttributeValue.GET("Item Attribute ID","Item Attribute Value ID");
             END;

    OnDelete=VAR
               ItemAttribute@1002 : Record 7500;
               ItemAttributeValue@1000 : Record 7501;
               ItemAttributeValueMapping@1001 : Record 7505;
             BEGIN
               ItemAttribute.GET("Item Attribute ID");
               IF ItemAttribute.Type = ItemAttribute.Type::Option THEN
                 EXIT;

               IF NOT ItemAttributeValue.GET("Item Attribute ID","Item Attribute Value ID") THEN
                 EXIT;

               ItemAttributeValueMapping.SETRANGE("Item Attribute ID","Item Attribute ID");
               ItemAttributeValueMapping.SETRANGE("Item Attribute Value ID","Item Attribute Value ID");
               IF ItemAttributeValueMapping.COUNT <> 1 THEN
                 EXIT;

               ItemAttributeValueMapping := Rec;
               IF ItemAttributeValueMapping.FIND THEN
                 ItemAttributeValue.DELETE;
             END;

    CaptionML=ENU=Item Attribute Value Mapping;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 2   ;   ;No.                 ;Code20        ;CaptionML=ENU=No. }
    { 3   ;   ;Item Attribute ID   ;Integer       ;TableRelation="Item Attribute";
                                                   CaptionML=ENU=Item Attribute ID }
    { 4   ;   ;Item Attribute Value ID;Integer    ;TableRelation="Item Attribute Value".ID;
                                                   CaptionML=ENU=Item Attribute Value ID }
  }
  KEYS
  {
    {    ;Table ID,No.,Item Attribute ID          ;Clustered=Yes }
    {    ;Item Attribute ID,Item Attribute Value ID }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE RenameItemAttributeValueMapping@1(PrevNo@1000 : Code[20];NewNo@1001 : Code[20]);
    VAR
      ItemAttributeValueMapping@1003 : Record 7505;
    BEGIN
      SETRANGE("Table ID",DATABASE::Item);
      SETRANGE("No.",PrevNo);
      IF FINDSET THEN
        REPEAT
          ItemAttributeValueMapping := Rec;
          ItemAttributeValueMapping.RENAME("Table ID",NewNo,"Item Attribute ID");
        UNTIL NEXT = 0;
    END;

    BEGIN
    END.
  }
}

